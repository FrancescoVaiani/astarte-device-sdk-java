plugins {
	id 'java-library'
}

sourceSets {
	intTest {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

configurations {
	intTestImplementation.extendsFrom implementation
	intTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
	api project(':DeviceSDK')

	implementation 'com.j256.ormlite:ormlite-core:5.1'
	api 'com.j256.ormlite:ormlite-jdbc:5.1'
	implementation 'org.mongodb:bson:3.11.0'

	intTestImplementation 'junit:junit:4.13'
	intTestImplementation 'org.hamcrest:hamcrest:2.2'
	intTestImplementation 'com.h2database:h2:1.4.200'
}

task integrationTest(type: Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.intTest.output.classesDirs
	classpath = sourceSets.intTest.runtimeClasspath
	shouldRunAfter test
}

check.dependsOn integrationTest

test {
	useJUnit()

	maxHeapSize = '1G'
}

ext {
	PUBLISH_GROUP_ID = project.group
	PUBLISH_VERSION = project.version
	PUBLISH_ARTIFACT_ID = 'devicesdk-generic'
}

apply from: "${rootProject.projectDir}/scripts/publish-module.gradle"
